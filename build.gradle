apply plugin: 'java'

group               = 'testgrp'
version             = '0.2.0'
status              = 'integration'
description         = 'TODO - Pls. edit - Gradle bootstrap project for Quasar'

sourceCompatibility = 1.8
targetCompatibility = 1.8

ext.quasarVer       = '0.7.5'
ext.junitVer        = '4.12'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

configurations {
    quasar
}

configurations.all {
    resolutionStrategy {
        failOnVersionConflict()
    }
}

repositories {
//    mavenLocal()
    mavenCentral()
//    maven { url "https://oss.sonatype.org/content/repositories/releases" }
//    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
//    maven { url 'https://maven.java.net/content/repositories/snapshots' }
}

dependencies {
    compile "co.paralleluniverse:quasar-core:${quasarVer}:jdk8"
    quasar "co.paralleluniverse:quasar-core:${quasarVer}:jdk8@jar"

    testCompile "junit:junit:$junitVer"
}

test {
    beforeTest { desc ->
        logger.quiet("Running test: " + desc)
    }

    afterTest { desc, result ->
        if(result.resultType == TestResult.ResultType.FAILURE) {
            logger.quiet("Failed test ${desc.name} [${desc.className}] with exception: ${result.exception}")
            if(result.exception != null) {
                result.exception.printStackTrace()
            }
        }
    }
}

testClasses {
    doFirst {
        println "Executing scanSuspendables..."
        ant.taskdef (
            name:'scanSuspendables', classname:'co.paralleluniverse.fibers.instrument.SuspendablesScanner',
            classpath: "build/classes/test:build/resources/test:${configurations.runtime.asPath}"
        )
        ant.scanSuspendables (
            auto: true,
            suspendablesFile: "$sourceSets.test.output.resourcesDir/META-INF/suspendables",
            supersFile: "$sourceSets.test.output.resourcesDir/META-INF/suspendable-supers",
            append: true
        ) {
            fileset(dir: sourceSets.test.output.classesDir)
        }
    }
}

tasks.withType(Test) {
    useJUnit()
    systemProperty 'co.paralleluniverse.fibers.verifyInstrumentation', 'true'

    doLast() {
        println "file://" + getReports().getHtml().getDestination() + "/index.html"
    }
}

tasks.withType(Test) {
    jvmArgs "-javaagent:${configurations.quasar.singleFile}=vdcmb" // =v, =d
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

defaultTasks 'test'
